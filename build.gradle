plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "edu.wpi.first.GradleRIO" version "2019.0.0-alpha-3"
    id "org.ajoberstar.grgit" version "3.0.0"
}

def TEAM = 4450
def ROBOT_CLASS = "Team4450.Robot19.Robot"

build.finalizedBy 'deploy'

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", edu.wpi.first.gradlerio.frc.RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', edu.wpi.first.gradlerio.frc.FRCJavaArtifact) {
            targets << "roborio"
        }
   }
}

// Setup eclipse classpath settings, so the javadoc and sources are available in eclipse
eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

// Look in local maven cache first, then maven central, then jitpack for dependent libraries.
repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'https://jitpack.io' }
}

// Defining dependencies. In this case, WPILib (+ friends), CTRE Phoenix (Talon SRX), NavX, RobotLib
// and the local pathfinder project jar. RobotLib is downloaded from Jitpack by version number or commit id. 
// Change 'local' to desired RobotLib version, commit id or master-SNAPSHOT for most recent.
dependencies {
    compile wpilib()
    compile "com.github.ORF-4450:RobotLib:v2.3"
}

// These next definitions (branch and hash) attempt to find extra information to add to the the manifest of
// the robot program jar file.

// Either the name of the current branch, or an empty string if no git repo is found.
def branch = { ->
    if (grgit == null) { // If there's no git repo.
        return ""
    }

    return grgit.branch.current().getName()
}

// Either the short hash of the last commit or an empty string if no git repo is found.
def hash = { ->
    if (grgit == null) { // If there's no git repo.
        return ""
    }
    
    return grgit.head().abbreviatedId
}

// Setting up robot program Jar File. In this case, adding all libraries into the main jar ('fat jar')
// in order to make them all available at runtime. Also adding the manifest so WPILib
// knows where to look for our Robot Class. Add version information to jar manifest.
jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
        manifest edu.wpi.first.gradlerio.GradleRIOPlugin.javaManifest(ROBOT_CLASS)
        
        manifest {
         	attributes("Branch": branch())
        	attributes("Commit": hash())
           	attributes("Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
        	attributes("User": System.getProperty("user.name"))
    	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}
